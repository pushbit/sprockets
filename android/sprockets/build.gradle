apply plugin: 'com.android.library'
apply plugin: 'maven'

def libName = 'Sprockets Android'
def libVersionCode = 3
def libVersionName = '2.2.0'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'
    defaultConfig {
        versionCode libVersionCode
        versionName libVersionName
        minSdkVersion 16
        targetSdkVersion 21
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

configurations {
    javadoc
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v13:21.0.3'
    compile 'com.android.support:palette-v7:21.0.3'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'net.sf.sprockets:sprockets:2.2.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.github.frankiesardo:icepick:2.3.6'
    compile 'com.github.frankiesardo:icepick-processor:2.3.6'
    compile 'commons-io:commons-io:2.4'

    /* Google repository dependencies aren't visible in configs, use external jars instead */
    def extras = "${android.sdkDirectory}/extras"
    javadoc fileTree(dir: "${extras}/android/support", include: ['**/*.jar'])
    javadoc fileTree(dir: "${extras}/google/google_play_services/libproject", include: ['**/*.jar'])
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${buildDir}/repo/")
            pom.project {
                groupId 'net.sf.sprockets'
                artifactId 'sprockets-android'
                version libVersionName
                packaging 'aar'
                name libName
                description 'Base components, widgets, and utility methods.'
                url 'https://pushbit.github.io/sprockets/android'
                licenses {
                    license {
                        name 'GNU Lesser General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/lgpl.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'pushbit'
                        name 'Paul Bitson'
                        email 'pushbit@gmail.com'
                    }
                }
                issueManagement {
                    system 'GitHub'
                    url 'https://github.com/pushbit/sprockets/issues'
                }
                scm {
                    connection 'scm:git:git://github.com/pushbit/sprockets.git'
                    url 'https://github.com/pushbit/sprockets'
                }
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task androidJavadocs(type: Javadoc, dependsOn: 'processReleaseResources') {
    source = android.sourceSets.main.java.srcDirs
            .plus(fileTree(dir: "${buildDir}/generated/source/r/release"))
            .plus(fileTree(dir: "../../java/src/main/java"))
    classpath += files(android.bootClasspath, configurations.compile, configurations.javadoc)
    title = "${libName} ${libVersionName} API"
    options {
        links 'http://docs.guava-libraries.googlecode.com/git-history/release/javadoc/',
                'https://commons.apache.org/proper/commons-configuration/javadocs/v1.10/apidocs/',
                'https://square.github.io/retrofit/javadoc/',
                'https://square.github.io/picasso/javadoc/',
                'https://jakewharton.github.io/butterknife/javadoc/'
        linksOffline 'https://developer.android.com/reference/',
                "${android.sdkDirectory}/docs/reference"
        stylesheetFile file('src/main/javadoc/stylesheet.css')
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

artifacts {
    archives androidSourcesJar, androidJavadocsJar
}
